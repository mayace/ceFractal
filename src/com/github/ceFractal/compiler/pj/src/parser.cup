package com.github.ceFractal.compiler.pj;

import com.github.gg.Dict;
import java.util.ArrayList;

//===============================================================================================//
//===============================================================================================//

parser code {: 
	public void report_error(String message, Object info){
        System.err.println(message);
    }

    public void report_fatal_error(String message,Object info){
        report_error(message,info);
    }
:};

action code{:

:}
   
//===============================================================================================//
//===============================================================================================//
terminal        String 		ID,STRING,EQUAL,TAGi,TAGf,TAGe,TAGi2
							;
non terminal    Dict        app,stmt,stmt_list,string,stmt_tag,tag_attr_list,tag_attrs,tag_attr,
							tag_body,tag_body_stmt,tag_body_stmt_list,id
							;
//===============================================================================================//
//===============================================================================================//

app			::=	stmt_list:l {:RESULT = l;:}	
				|			{:RESULT = new Dict(){{put("list",new ArrayList<Dict>());}};:}
				;
stmt_list	::=	stmt_list:l stmt:x
				{:
					l.getDictArrayList("list").add(x);
					RESULT = l;
				:}
				|stmt:x
				{:
					RESULT = new Dict(){{
						put("list",new ArrayList<Dict>(){{add(x);}});
					}};
				:}
				;

stmt 		::=	stmt_tag:x	{:RESULT = x;:}
				;

stmt_tag	::=	TAGi:t1 tag_attrs:x TAGi2 tag_body:b TAGf:t2
				{:
					String name = t1.substring(1);
					if(name.equalsIgnoreCase(t2.substring(2,t2.lastIndexOf('>')))){
						System.err.println("Normal Tag Name -> " + name);
					}

					RESULT = new Dict(){{
						put("is_tag",true);
						put("tag","normal");
						put("tagname",name);
						put("attrs",x);
						put("body",b);
					}};
				:}
				|TAGi:t1 tag_attrs:x TAGe:t2
				{:
					String name = t1.substring(1);
					System.err.println("Short Tag Name -> " + name);

					RESULT = new Dict(){{
						put("is_tag",true);
						put("tag","short");
						put("tagname",name);
						put("attrs",x);
						put("body",new Dict(){{put("list",new ArrayList<Dict>());}});
					}};
				:}
				;


tag_attrs		::=	tag_attr_list:l {:RESULT = l;:}
					| 				{:RESULT = new Dict(){{put("list",new ArrayList<Dict>());}};:}
					;
tag_attr_list	::=	tag_attr_list:l tag_attr:x
					{:
						l.getDictArrayList("list").add(x);
						RESULT = l;
					:}
					|tag_attr:x
					{:
						RESULT = new Dict(){{
							put("list",new ArrayList<Dict>(){{add(x);}});
						}};
					:}
					;

tag_attr 		::= id:x EQUAL string:v
					{:
						RESULT = new Dict(){{
							put("is_attr",true);
							put("id",x);
							put("val",v);
						}};
					:}
					;

tag_body 	::= tag_body_stmt_list:l 	{:RESULT = l;:}
				|						{:RESULT = new Dict(){{put("list",new ArrayList<Dict>());}};:}
				;

tag_body_stmt_list 	::=	tag_body_stmt_list:l tag_body_stmt:x
						{:
							l.getDictArrayList("list").add(x);
							RESULT = l;
						:}
						|tag_body_stmt:x 
						{:
							RESULT = new Dict(){{
								put("list",new ArrayList<Dict>(){{add(x);}});
							}};
						:}
						;

tag_body_stmt 		::= stmt_tag:x	
						{:RESULT = x;:}
						;

id 			::= ID:x
				{:
					RESULT = new Dict(){{
						put("val",x);
					}};
				:}
				;
string 		::= STRING:x
				{:
					RESULT = new Dict(){{
						put("val",x);
					}};
				:}
				;